buildscript {
    repositories mavenRepositories
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
    }
}

normalization {
    runtimeClasspath {
        ignore("**/*git.properties*")
        ignore("**/*build-info.properties*")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply from: "$projectDir/gradle/lite.gradle"
apply plugin: 'com.google.cloud.tools.jib'
apply from: "../gradle/jib.gradle"
apply plugin: "java-library"

def debugPort = 5120
def componentName = "gateway-service"

setJib(componentName, debugPort)
jib.container.mainClass = 'org.zowe.apiml.gateway.GatewayApplication'

springBoot {
    // This statement tells the Gradle Spring Boot plugin to generate a file
    // build/resources/main/META-INF/build-info.properties that is picked up by Spring Boot to display
    // via /info endpoint
    buildInfo {
        properties {
            // Generate extra build info:
            additionalProperties = [
                by             : System.properties['user.name'],
                operatingSystem: "${System.properties['os.name']} (${System.properties['os.version']})",
                number         : System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : "n/a",
                machine        : InetAddress.localHost.hostName
            ]
        }
    }
}

gitProperties {
    gitPropertiesDir = new File("${project.rootDir}/${name}/build/resources/main/META-INF")
}

configurations.all {
    resolutionStrategy {
        force (libraries.jetty_websocket_client)
    }
}

dependencies {
    api(project(':apiml-security-common'))
    api libraries.spring_cloud_starter_zuul
    api libraries.spring_boot_starter_actuator
    api libraries.spring_boot_starter_web
    api libraries.spring_boot_starter_websocket
    api libraries.spring_boot_starter_thymeleaf
    api libraries.spring_boot_starter_cache
    api libraries.spring_boot_starter_aop
    api libraries.spring_security_web
    api libraries.spring_security_config
    api libraries.swagger3_core
    api libraries.swagger3_parser
    api libraries.jackson_core
    api libraries.tomcat_coyote
    api libraries.tomcat_embed_core
    api libraries.spring_cloud_starter_eureka
    api libraries.spring_cloud_starter_ribbon
    api libraries.jetty_websocket_common
    api libraries.jetty_websocket_client
    api libraries.jetty_client
    api libraries.jetty_http
    api libraries.jetty_io
    api libraries.jetty_util
    api libraries.jetty_websocket_api
    api libraries.jjwt
    api libraries.nimbusJoseJwt
    api libraries.eh_cache
    api libraries.spring_retry
    api libraries.awaitility

    compileOnly libraries.javax_inject
    compileOnly libraries.lombok
    annotationProcessor libraries.lombok

    api libraries.springFox
    testCompile libraries.spring_mock_mvc
    testCompile libraries.spring_boot_starter_test
    testRuntime libraries.http_client
    testCompile libraries.rest_assured
    testCompile libraries.javax_inject

    testCompile libraries.lombok
    testAnnotationProcessor libraries.lombok


    runtime libraries.jjwt_impl
    runtime libraries.jjwt_jackson
}

bootJar {
    archiveFileName = "gateway-service.jar"
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
    archiveClassifier = "application"
}

jar {
    enabled true
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
    systemProperties = System.properties
}

publishing {
    publications {
        mavenJavaLib(MavenPublication) {
            artifact jar
        }

        mavenJavaFat(MavenPublication) {
            groupId = 'org.zowe.apiml.sdk'
            artifactId = 'gateway-fat-jar'

            artifact bootJar
        }
    }
}
